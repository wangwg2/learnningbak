ant.importBuild '../ant/build.xml'

[init, compile, dist]*.logging*.level = LogLevel.INFO

init {
    doFirst {
        logger.quiet "Deleting the directory '${ant.properties.build}'."
    }

    doLast {
        logger.quiet "Starting from a clean slate."
    }
}

ext.antBuildDir = '../ant/build'
ant.properties.build = "$antBuildDir/classes"
ant.properties.dist = "$antBuildDir/libs"

task sourcesJar(type: Jar) {
    baseName = 'my-app'
    classifier = 'sources'
    version = ant.properties.version
    destinationDir = file(ant.properties.dist)
    from new File(ant.properties.src, 'main/java')
}

dist.dependsOn sourcesJar

compile {
    inputs.dir file(ant.properties.src)
    outputs.dir file(ant.properties.build)
}

task downloadReleaseDocumentation {
    logging.level = LogLevel.INFO
    ext.repoUrl = 'https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter09/listing_09_05-06-migrating-ant-build'

    doLast {
        ant.get(dest: ant.properties.build) {
            url(url: "$repoUrl/files/readme.txt")
            url(url: "$repoUrl/files/release_notes.txt")
        }
    }
}

dist.dependsOn downloadReleaseDocumentation

configurations {
    antCompile
}

repositories {
    mavenCentral()
}

dependencies {
    antCompile 'org.apache.commons:commons-lang3:3.1'
}

ant.properties.antCompileClasspath = configurations.antCompile.asPath
