repositories {
    mavenCentral()

    ivy {
        name 'JSHint'
        url 'https://raw.githubusercontent.com/bmuschko/gradle-in-action-source/master/chapter11/listing_11_06-07-todo-js-code-quality/ivy-repo'
        layout 'pattern', {
            artifact '[organization]/[module]/[module]-[revision](.[classifier]).[ext]'
        }
    }
}

configurations {
    rhino
    jshint
}

dependencies {
    rhino 'org.mozilla:rhino:1.7R4'
    jshint 'jshint-rhino:jshint-rhino:2.1.11@js'
}

ext.jsSourceDir = "$webAppDir/js/app"
def jsSourceFiles = fileTree(jsSourceDir).include('*.js')
def jsHintArgs = configurations.jshint + jsSourceFiles

task jsHint(type: JavaExec) {
    classpath configurations.rhino
    main 'org.mozilla.javascript.tools.shell.Main'
    args jsHintArgs
}
