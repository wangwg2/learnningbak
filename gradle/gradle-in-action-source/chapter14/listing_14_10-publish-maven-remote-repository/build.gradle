apply plugin: 'groovy'

archivesBaseName = 'cloudbees-plugin'
group = 'com.manning.gia'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.cloudbees:cloudbees-api-client:1.4.0'
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

task sourcesJar(type: Jar) {
    baseName 'cloudbees-plugin'
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    baseName 'cloudbees-plugin'
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

apply plugin: 'distribution'

distributions {
    main {
        baseName = archivesBaseName

        contents {
            from { libsDir }
        }
    }

    docs {
        baseName = "$archivesBaseName-docs"

        contents {
            from(libsDir) {
                include sourcesJar.archiveName
                include groovydocJar.archiveName
            }
        }
    }
}

ext {
    artifactoryBaseUrl = 'http://localhost:8081/artifactory'
    artifactorySnapshotRepoUrl = "$artifactoryBaseUrl/libs-snapshot-local"
    artifactoryReleaseRepoUrl = "$artifactoryBaseUrl/libs-release-local"
}

apply plugin: 'maven-publish'

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifactId 'cloudbees-plugin'

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle CloudBees plugin')
                root.appendNode('description', 'Gradle plugin for managing applications and databases on CloudBees RUN@cloud.')
                root.appendNode('inceptionYear', '2013')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'bmuschko')
                developer.appendNode('name', 'Benjamin Muschko')
                developer.appendNode('email', 'benjamin.muschko@gmail.com')
            }

            artifact sourcesJar
            artifact groovydocJar
        }
    }

    repositories {
        maven {
            name 'myLocal'
            url "file://$projectDir/repo"
        }

        maven {
            name 'remoteArtifactory'
            url project.version.endsWith('-SNAPSHOT') ? artifactorySnapshotRepoUrl : artifactoryReleaseRepoUrl

            credentials {
                username = artifactoryUsername
                password = artifactoryPassword
            }
        }
    }
}