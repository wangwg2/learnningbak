apply plugin: 'war'
apply plugin: 'jetty'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':repository')
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
}

task createBuildInfoFile << {
    def buildInfoFile = new File("$buildDir/build-info.properties")
    Properties props = new Properties()
    props.setProperty('version', project.version.toString())
    props.setProperty('timestamp', project.buildTimestamp)
    props.store(buildInfoFile.newWriter(), null)
}

war {
    dependsOn createBuildInfoFile
    baseName = 'todo'

    from(buildDir) {
        include 'build-info.properties'
        into('WEB-INF/classes')
    }
}

apply plugin: 'maven-publish'

ext {
    artifactoryBaseUrl = 'http://localhost:8081/artifactory'
    artifactoryReleaseRepoUrl = "$artifactoryBaseUrl/libs-release-local"
    artifactoryUsername = project.hasProperty('artifactoryUsername') ? project.getProperty('artifactoryUsername') : null
    artifactoryPassword = project.hasProperty('artifactoryPassword') ? project.getProperty('artifactoryPassword') : null
}

publishing {
    publications {
        toDoWebApp(MavenPublication) {
            from components.web
            artifactId 'todo-web'

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'To Do application')
                root.appendNode('description', 'A simple task management application.')
            }
        }
    }

    repositories {
        maven {
            name 'remoteArtifactory'
            url artifactoryReleaseRepoUrl

            credentials {
                username = artifactoryUsername
                password = artifactoryPassword
            }
        }
    }
}

